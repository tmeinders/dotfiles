# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files


source ~/.dotfiles/antigen/antigen.zsh
antigen use oh-my-zsh
antigen bundle heroku
antigen bundle vagrant
antigen bundle zsh-users/zsh-syntax-highlighting
antigen bundle history-substring-search
antigen bundle tmuxinator
antigen theme mh
antigen apply

bindkey -v 
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down

unalias run-help
autoload run-help
HELPDIR=/usr/local/share/zsh/help

# handy gui function for kicking off and backgrounding graphical application


gui ()
{
	if [ $# -gt 0 ] ; then
		#($@ &) &>/dev/null
		($@ &) &
	else
		echo "missing argument"
	fi
}

